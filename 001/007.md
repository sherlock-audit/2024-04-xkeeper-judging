Gigantic Sand Albatross

medium

# Gelato's tasks without fees are not supported

## Summary

The AutomationVault contract doesn't handle the execution of Gelato's task when no fees is set.

## Vulnerability Detail

All the relay except `GelatoRelay` use one of the following configuration:
- `Keep3rBondedRelay` and `Keep3rRelay`: they both use no fees, so they set an empty `FeeData` array
- `OpenRelay`: it calculates a fee paid in native token

However, the `GelatorRelay` behaves differently.
When no fees is set on a Gelato-related task, the `FeeData` array contains one element
with `token = address(0)` and `fee = 0`.

The issue relies in `AutomationVault` which will not handle this zero address case.
It will try executing the transfer of `0` tokens by interacting with `address(0)`.
However, as `SafeERC20` library is used, the call will revert and task
with zero fees from Gelato will never be executable.


There are a lot of mainnet transactions showing no fees on Gelato tasks.
[0xc76db7292b3aa40af5d1913c43a14e532a160e020d6388e2f2c1fea03df16245](https://etherscan.io/tx/0xc76db7292b3aa40af5d1913c43a14e532a160e020d6388e2f2c1fea03df16245)
is an example of a `createTask` instruction with `feeToken = address(0)`.

## Impact

DoS issue: Task with zero fees from Gelato are not be executable.

## Code Snippet

The [Gelato's `Automate` contract's implementation](https://etherscan.io/address/0x6948f858af85505789259cc610186b36cee53683#code) shows the following code when `exec()` is called.
```solidity
    ///@inheritdoc IAutomate
    function exec(
        address _taskCreator,
        address _execAddress,
        bytes memory _execData,
        LibDataTypes.ModuleData calldata _moduleData,
        uint256 _txFee,
        address _feeToken,
        bool _revertOnFailure
    ) external onlyGelato {
        bytes32 taskId = LibTaskId.getTaskId(
            _taskCreator,
            _execAddress,
            _execData.memorySliceSelector(),
            _moduleData,
            _feeToken
        );

        require(
            _createdTasks[_taskCreator].contains(taskId),
            "Automate.exec: Task not found"
        );

        fee = _txFee;
        feeToken = _feeToken; // @POC: Set the `_feeToken` controlled by user
```

As we can see, the `feeToken` storage variable is set at the start of the function and is controlled by the caller.


[The `GelatoRelay.exec` function](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/relays/GelatoRelay.sol#L30-L37) encodes the fee token and the fee amount in a `FeeData` array element
and uses it as input to `AutomationVault.exec`:
```solidity
  function exec(IAutomationVault _automationVault, IAutomationVault.ExecData[] calldata _execData) external {
    // Get the fee details
    (uint256 _fee, address _feeToken) = AUTOMATE.getFeeDetails(); // @POC: retrieves the `feeToken` and `fee` from Automate

    // Create fee data
    IAutomationVault.FeeData[] memory _feeData = new IAutomationVault.FeeData[](1);
    _feeData[0] = IAutomationVault.FeeData(FEE_COLLECTOR, _feeToken, _fee);

    // Execute the automation vault
    _automationVault.exec(msg.sender, _execData, _feeData);

    // Emit the event
    emit AutomationVaultExecuted(_automationVault, msg.sender, _execData, _feeData);
  }
```


Finally, the [`AutomationVault.exec` function interacts with the fee token](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L442) without checking if the token address is not the zero address.

```solidity
  function exec(address _relayCaller, ExecData[] calldata _execData, FeeData[] calldata _feeData) external {
    // @POC: REDACTED - EXECUTION PART

    // Create the fee data needed variables
    FeeData memory _feeInfo;
    _dataLength = _feeData.length;
    _i = 0;

    // Iterate over the fee data to issue the payments
    for (_i; _i < _dataLength;) {
      _feeInfo = _feeData[_i];// @POC: Each element of the `_feeData` array is processed

      // If the token is the native token, transfer the funds to the receiver, otherwise transfer the tokens
      if (_feeInfo.feeToken == NATIVE_TOKEN) {
        (_success,) = _feeInfo.feeRecipient.call{value: _feeInfo.fee}('');
        if (!_success) revert AutomationVault_NativeTokenTransferFailed();
      } else {
        IERC20(_feeInfo.feeToken).safeTransfer(_feeInfo.feeRecipient, _feeInfo.fee);// @POC: Each feeToken address of the `_feeData` array is called without checking address(0)
      }

      // Emit the event
      emit IssuePayment(msg.sender, _relayCaller, _feeInfo.feeRecipient, _feeInfo.feeToken, _feeInfo.fee);

      unchecked {
        ++_i;
      }
    }
  }
```

This means that if the task executor which calls `Automate.exec` doesn't require fees, the task will not be executable.

## Proof of Concept

The following Proof of Concept can be added to `solidity/test/integration/GelatoRelay.t.sol`:

```solidity

  function _createTaskNoFee() internal returns (bytes32 _taskId) {
    LibDataTypes.ModuleData memory _moduleData = _createModuleData();

    _taskId = automate.createTask(
      address(gelatoRelay), abi.encodeWithSelector(gelatoRelay.exec.selector), _moduleData, address(0x0)
    );
  }

    function test_executeJobGelatoNoFees() public {
    changePrank(owner);
    _createTaskNoFee();

    changePrank(address(gelato));

    IAutomationVault.ExecData[] memory _execData = new IAutomationVault.ExecData[](1);
    _execData[0] = IAutomationVault.ExecData(address(basicJob), abi.encodeWithSelector(basicJob.work.selector));

    // Create exec data for Automate
    bytes memory _execDataAutomate = abi.encodeWithSelector(gelatoRelay.exec.selector, automationVault, _execData);

    // Create module data for Automate
    LibDataTypes.ModuleData memory _moduleData = _createModuleData();

    // Execute in Automate and expect Gelato to execute the job
    vm.expectRevert("Automate.exec: OpsProxy.executeCall: Address: call to non-contract");
    automate.exec(owner, address(gelatoRelay), _execDataAutomate, _moduleData, 0, address(0x0), true);
  }
```

## Tool used

Manual Review

## Recommendation

The issue can be fixed through 2 efficient ways:
- **Fix 1 -** In `GelatoRelay.exec`: Handling of the zero address by creating an empty `FeeData` array
- **Fix 2 -** In `AutomationVault.exec`: Ignoring the token address call when it is set to the zero address

**Fix 1:**

```diff
diff --git a/xkeeper-core/solidity/contracts/relays/GelatoRelay.sol b/xkeeper-core/solidity/contracts/relays/GelatoRelay.sol
index c769dac..44c7e6a 100644
--- a/xkeeper-core/solidity/contracts/relays/GelatoRelay.sol
+++ b/xkeeper-core/solidity/contracts/relays/GelatoRelay.sol
@@ -30,8 +30,14 @@ contract GelatoRelay is IGelatoRelay {
     (uint256 _fee, address _feeToken) = AUTOMATE.getFeeDetails();
 
     // Create fee data
-    IAutomationVault.FeeData[] memory _feeData = new IAutomationVault.FeeData[](1);
-    _feeData[0] = IAutomationVault.FeeData(FEE_COLLECTOR, _feeToken, _fee);
+    IAutomationVault.FeeData[] memory _feeData;
+    if (_feeToken == address(0)) {
+      _feeData = new IAutomationVault.FeeData[](0);
+    }
+    else {
+      _feeData = new IAutomationVault.FeeData[](1);
+      _feeData[0] = IAutomationVault.FeeData(FEE_COLLECTOR, _feeToken, _fee);
+    }
 
     // Execute the automation vault
     _automationVault.exec(msg.sender, _execData, _feeData);
 
```


**Fix 2:**

```diff
diff --git a/xkeeper-core/solidity/contracts/core/AutomationVault.sol b/xkeeper-core/solidity/contracts/core/AutomationVault.sol
index 82c3925..ceac9f0 100644
--- a/xkeeper-core/solidity/contracts/core/AutomationVault.sol
+++ b/xkeeper-core/solidity/contracts/core/AutomationVault.sol
@@ -438,7 +438,7 @@ contract AutomationVault is IAutomationVault {
       if (_feeInfo.feeToken == NATIVE_TOKEN) {
         (_success,) = _feeInfo.feeRecipient.call{value: _feeInfo.fee}('');
         if (!_success) revert AutomationVault_NativeTokenTransferFailed();
-      } else {
+      } else if (_feeInfo.feeToken != address(0)) {
         IERC20(_feeInfo.feeToken).safeTransfer(_feeInfo.feeRecipient, _feeInfo.fee);
       }
 
```


*Note: The patch can be applied through `git apply`.*