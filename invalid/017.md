Gigantic Sand Albatross

medium

# XKeeperMetadata allows registering metadata for untrusted AutomationVaults

## Summary

XKeeperMetadata doesn't check if the AutomationVault for which metadata is registered is a legit one deployed through the Factory. 

## Vulnerability Detail

The registration of metadata for an AutomationVault is done in the `XKeeperMetadata` contract by the owner of the vault. This registration is achieved through a call to `XKeeperMetadata.setAutomationVaultMetadata`.

The function calls the `_automationVault` to ensure that the owner is the current `msg.sender`.

However, the function doesn't ensure that the `_automationVault` passed as parameters has been deployed through the `AutomationVaultFactory` contract.

An attacker can deploy his own malicious `AutomationVault` without interacting with the factory, and he will be able to register metadata in the `XKeeperMetadata` contract for this malicious vault.


## Impact

The `XKeeperMetadata` contract can't be trusted to identify legit `AutomationVault` contracts.

## Code Snippet

The [`XKeeperMetadata.setAutomationVaultMetadata` function](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/periphery/XKeeperMetadata.sol#L41-L57) doesn't check the its input `_automationVault` has been deployed through the factory.

```solidity
  function setAutomationVaultMetadata(
    IAutomationVault _automationVault,
    IXKeeperMetadata.AutomationVaultMetadata calldata _automationVaultMetadata
  ) external {
    // Check if the caller is the owner of the automation vault
    if (_automationVault.owner() != msg.sender) {
      revert XKeeperMetadata_OnlyAutomationVaultOwner();
    }

    // Index the metadata with the automation vault
    automationVaultMetadata[_automationVault] = _automationVaultMetadata;

    // Emit an event when the metadata is set
    emit AutomationVaultMetadataSetted(
      _automationVault, _automationVaultMetadata.name, _automationVaultMetadata.description
    );
  }
```


## Tool used

Manual Review

## Recommendation

The `XKeeperMetadata` should interact with the `AutomationVaultFactory` to ensure that the vaults are legit ones.

The following patch implements such a fix by:
- Adding a `isDeployedFromFactory` function in `AutomationVaultFactory`.
- Adding an immutable `factory` variable in `XKeeperMetadata`, it is initialized during deployment through the constructor.
- Adding a check in `XKeeperMetadata` to ensure the vault was deployed by the factory, an error with a revert is executed if not.
- Adapting the `Deploy.s.sol` script

*Note that the test folder has not been modified, tests implying the `XKeeperMetadata` will fail.*


```diff
diff --git a/xkeeper-core/solidity/contracts/core/AutomationVaultFactory.sol b/xkeeper-core/solidity/contracts/core/AutomationVaultFactory.sol
index f149299..1cb26f1 100644
--- a/xkeeper-core/solidity/contracts/core/AutomationVaultFactory.sol
+++ b/xkeeper-core/solidity/contracts/core/AutomationVaultFactory.sol
@@ -49,6 +49,11 @@ contract AutomationVaultFactory is IAutomationVaultFactory {
     return _list;
   }
 
+  /// @inheritdoc IAutomationVaultFactory
+  function isDeployedFromFactory(address _automationVault) external view returns (bool _isFromFactory) {
+    _isFromFactory = _automationVaults.contains(_automationVault);
+  }
+
   /// @inheritdoc IAutomationVaultFactory
   function deployAutomationVault(
     address _owner,
diff --git a/xkeeper-core/solidity/contracts/periphery/XKeeperMetadata.sol b/xkeeper-core/solidity/contracts/periphery/XKeeperMetadata.sol
index a7c153d..aefaa9f 100644
--- a/xkeeper-core/solidity/contracts/periphery/XKeeperMetadata.sol
+++ b/xkeeper-core/solidity/contracts/periphery/XKeeperMetadata.sol
@@ -5,6 +5,7 @@ import {EnumerableSet} from 'openzeppelin/utils/structs/EnumerableSet.sol';
 
 import {IXKeeperMetadata} from '../../interfaces/periphery/IXKeeperMetadata.sol';
 import {IAutomationVault} from '../../interfaces/core/IAutomationVault.sol';
+import {IAutomationVaultFactory} from '../../interfaces/core/IAutomationVaultFactory.sol';
 
 /**
  * @title  XKeeperMetadata
@@ -14,10 +15,16 @@ import {IAutomationVault} from '../../interfaces/core/IAutomationVault.sol';
 contract XKeeperMetadata is IXKeeperMetadata {
   using EnumerableSet for EnumerableSet.AddressSet;
 
+  IAutomationVaultFactory immutable factory;
+
   /// @inheritdoc IXKeeperMetadata
   mapping(IAutomationVault _automationVault => IXKeeperMetadata.AutomationVaultMetadata _automationVaultMetadata) public
     automationVaultMetadata;
 
+  constructor(IAutomationVaultFactory _factory) {
+    factory = _factory;
+  }
+
   /// @inheritdoc IXKeeperMetadata
   function automationVaultsMetadata(IAutomationVault[] calldata _automationVault)
     external
@@ -42,6 +49,9 @@ contract XKeeperMetadata is IXKeeperMetadata {
     IAutomationVault _automationVault,
     IXKeeperMetadata.AutomationVaultMetadata calldata _automationVaultMetadata
   ) external {
+    if (!factory.isDeployedFromFactory(address(_automationVault))) {
+      revert XKeeperMetadata_OnlyAutomationVaultFromFactory();
+    }
     // Check if the caller is the owner of the automation vault
     if (_automationVault.owner() != msg.sender) {
       revert XKeeperMetadata_OnlyAutomationVaultOwner();
diff --git a/xkeeper-core/solidity/interfaces/core/IAutomationVaultFactory.sol b/xkeeper-core/solidity/interfaces/core/IAutomationVaultFactory.sol
index ab9a684..546aa29 100644
--- a/xkeeper-core/solidity/interfaces/core/IAutomationVaultFactory.sol
+++ b/xkeeper-core/solidity/interfaces/core/IAutomationVaultFactory.sol
@@ -42,6 +42,13 @@ interface IAutomationVaultFactory {
    */
   function automationVaults(uint256 _startFrom, uint256 _amount) external view returns (address[] memory _list);
 
+  /**
+   * @notice Validate that an automation vault was deployed by the factory
+   * @param  _automationVault address of the automation vault
+   * @return _isFromFactory The boolean indicating if the vault has been deployed from the factory
+   */
+  function isDeployedFromFactory(address _automationVault) external view returns (bool _isFromFactory);
+
   /*///////////////////////////////////////////////////////////////
                           EXTERNAL FUNCTIONS
   //////////////////////////////////////////////////////////////*/
diff --git a/xkeeper-core/solidity/script/Deploy.s.sol b/xkeeper-core/solidity/script/Deploy.s.sol
index 7245dc2..f00e7d9 100644
--- a/xkeeper-core/solidity/script/Deploy.s.sol
+++ b/xkeeper-core/solidity/script/Deploy.s.sol
@@ -63,7 +63,7 @@ abstract contract Deploy is Script {
     }
 
     // Deploy metadata contract
-    xKeeperMetadata = new XKeeperMetadata{salt: _salt}();
+    xKeeperMetadata = new XKeeperMetadata{salt: _salt}(automationVaultFactory);
 
     vm.stopBroadcast();
   }

```

*Note: The patch can be applied through `git apply`.*