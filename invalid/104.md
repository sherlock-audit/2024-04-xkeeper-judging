Shallow Cobalt Turkey

medium

# The owner of an `AutomationVault` can alter an unauthorized relay.

## Summary

They don't check if the `_relay` was previously authorized in [AutomationVault.modifyRelayCallers()](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L283-L316) and [AutomationVault.modifyRelayJobs()](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L319-L394).

## Vulnerability Detail

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L283-L316

```solidity
  function modifyRelayCallers(address _relay, address[] memory _callers) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
    // Create the counter variable
    uint256 _i;

    // Get the approved callers
    address[] memory _oldApprovedCallers = _approvedCallers[_relay].values();

    // Get the length of the approved callers array
    uint256 _callersLength = _oldApprovedCallers.length;

    // Remove the callers
    for (_i; _i < _callersLength;) {
      _approvedCallers[_relay].remove(_oldApprovedCallers[_i]);

      unchecked {
        ++_i;
      }
    }

    // Get the length of the callers array
    _callersLength = _callers.length;

    // Set the callers for the relay
    for (_i = 0; _i < _callersLength;) {
      if (_approvedCallers[_relay].add(_callers[_i])) {
        emit ApproveRelayCaller(_relay, _callers[_i]);
      }

      unchecked {
        ++_i;
      }
    }
  }
```

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L319-L394

```solidity
  function modifyRelayJobs(address _relay, IAutomationVault.JobData[] memory _jobsData) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
    // Create the counters variables
    uint256 _i;
    uint256 _j;

    // Get the list of jobs
    address[] memory _jobs = _approvedJobs[_relay].values();

    // Get the length of the jobs array
    uint256 _jobsLength = _jobs.length;

    // Create the selector length variable
    bytes32[] memory _selectors;

    // Create the length variable
    uint256 _selectorsLength;

    // Remove the jobs
    for (_i; _i < _jobsLength;) {
      _approvedJobs[_relay].remove(_jobs[_i]);

      // Get the length of the selectors array
      _selectorsLength = _approvedJobSelectors[_relay][_jobs[_i]].length();

      // Get the list of selectors
      _selectors = _approvedJobSelectors[_relay][_jobs[_i]].values();

      // Remove the selectors
      for (_j = 0; _j < _selectorsLength;) {
        _approvedJobSelectors[_relay][_jobs[_i]].remove(_selectors[_j]);

        unchecked {
          ++_j;
        }
      }

      unchecked {
        ++_i;
      }
    }

    // Get the length of the jobs data array
    _jobsLength = _jobsData.length;

    // Set the jobs and their selectors for the relay
    for (_i = 0; _i < _jobsLength;) {
      IAutomationVault.JobData memory _jobData = _jobsData[_i];

      // Necessary to avoid an empty job from being assigned to selectors
      if (_jobData.job != address(0)) {
        // Set the job for the relay
        if (_approvedJobs[_relay].add(_jobData.job)) {
          emit ApproveJob(_jobData.job);
        }

        // Get the length of the selectors array
        _selectorsLength = _jobData.functionSelectors.length;

        // Set the selectors for the job
        for (_j = 0; _j < _selectorsLength;) {
          if (_approvedJobSelectors[_relay][_jobData.job].add(_jobData.functionSelectors[_j])) {
            emit ApproveJobSelector(_jobData.job, _jobData.functionSelectors[_j]);
          }

          unchecked {
            ++_j;
          }
        }
      }

      unchecked {
        ++_i;
      }
    }
  }
```

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L397-L452

```solidity
  function exec(address _relayCaller, ExecData[] calldata _execData, FeeData[] calldata _feeData) external {
    // Check that the specific caller is approved to call the relay
    if (!_approvedCallers[msg.sender].contains(_relayCaller) && !_approvedCallers[msg.sender].contains(_ALL)) {
      revert AutomationVault_NotApprovedRelayCaller();
    }

    // Create the exec data needed variables
    ExecData memory _dataToExecute;
    uint256 _dataLength = _execData.length;
    uint256 _i;
    bool _success;

    // Iterate over the exec data to execute the jobs
    for (_i; _i < _dataLength;) {
      _dataToExecute = _execData[_i];

      // Check that the selector is approved to be called
      if (!_approvedJobSelectors[msg.sender][_dataToExecute.job].contains(bytes4(_dataToExecute.jobData))) {
        revert AutomationVault_NotApprovedJobSelector();
      }
      (_success,) = _dataToExecute.job.call(_dataToExecute.jobData);
      if (!_success) revert AutomationVault_ExecFailed();

      // Emit the event
      emit JobExecuted(msg.sender, _relayCaller, _dataToExecute.job, _dataToExecute.jobData);

      unchecked {
        ++_i;
      }
    }

    // Create the fee data needed variables
    FeeData memory _feeInfo;
    _dataLength = _feeData.length;
    _i = 0;

    // Iterate over the fee data to issue the payments
    for (_i; _i < _dataLength;) {
      _feeInfo = _feeData[_i];

      // If the token is the native token, transfer the funds to the receiver, otherwise transfer the tokens
      if (_feeInfo.feeToken == NATIVE_TOKEN) {
        (_success,) = _feeInfo.feeRecipient.call{value: _feeInfo.fee}('');
        if (!_success) revert AutomationVault_NativeTokenTransferFailed();
      } else {
        IERC20(_feeInfo.feeToken).safeTransfer(_feeInfo.feeRecipient, _feeInfo.fee);
      }

      // Emit the event
      emit IssuePayment(msg.sender, _relayCaller, _feeInfo.feeRecipient, _feeInfo.feeToken, _feeInfo.fee);

      unchecked {
        ++_i;
      }
    }
  }
```

Due to the lack of validation to ensure whether the `_relay` has already been authorized, the owner of the `AutomationVault` can approve certain `caller`s and `job`s for an unauthorized relay. Consequently, a malicious owner could mislead a caller into executing [AutomationVault.exec()](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L397-L452) using an unauthorized relay, because there is also no check for the relay(`msg.sender`) in [AutomationVault.exec()](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L397-L452).

## Impact

The owner of an `AutomationVault` can assign certain callers to an unauthorized relay, deceiving callers into calling [AutomationVault.exec()](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L397-L452) through that relay, resulting in losses for the callers.

## Code Snippet

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L283-L316

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L319-L394

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L397-L452

## Tool used

Manual Review

## Recommendation

There should be a check for the relay in [AutomationVault.modifyRelayCallers()](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L283-L316), [AutomationVault.modifyRelayJobs()](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L319-L394) and [AutomationVault.exec()](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L397-L452). For example:

```diff
  function modifyRelayCallers(address _relay, address[] memory _callers) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
+   if(!_relays.contains(_relay)) revert("Unauthorized relay!");
    // Create the counter variable
    uint256 _i;

    // Get the approved callers
    address[] memory _oldApprovedCallers = _approvedCallers[_relay].values();

    // Get the length of the approved callers array
    uint256 _callersLength = _oldApprovedCallers.length;

    // Remove the callers
    for (_i; _i < _callersLength;) {
      _approvedCallers[_relay].remove(_oldApprovedCallers[_i]);

      unchecked {
        ++_i;
      }
    }

    // Get the length of the callers array
    _callersLength = _callers.length;

    // Set the callers for the relay
    for (_i = 0; _i < _callersLength;) {
      if (_approvedCallers[_relay].add(_callers[_i])) {
        emit ApproveRelayCaller(_relay, _callers[_i]);
      }

      unchecked {
        ++_i;
      }
    }
  }
```