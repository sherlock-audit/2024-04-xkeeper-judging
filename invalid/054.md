Zany Pine Boar

medium

# No check for `_relay` in `AutomationVault::modifyRelayCallers`, `AutomationVault::modifyRelayJobs` and `AutomationVault::exec`.

## Summary

In `AutomationVault::modifyRelayCallers`, `AutomationVault::modifyRelayJobs` and `AutomationVault::exec`, there is no verification to determine if the `_relay` has already been registered.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L283-L316

```solidity
  function modifyRelayCallers(address _relay, address[] memory _callers) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
    // Create the counter variable
    uint256 _i;

    // Get the approved callers
    address[] memory _oldApprovedCallers = _approvedCallers[_relay].values();

    // Get the length of the approved callers array
    uint256 _callersLength = _oldApprovedCallers.length;

    // Remove the callers
    for (_i; _i < _callersLength;) {
      _approvedCallers[_relay].remove(_oldApprovedCallers[_i]);

      unchecked {
        ++_i;
      }
    }

    // Get the length of the callers array
    _callersLength = _callers.length;

    // Set the callers for the relay
    for (_i = 0; _i < _callersLength;) {
      if (_approvedCallers[_relay].add(_callers[_i])) {
        emit ApproveRelayCaller(_relay, _callers[_i]);
      }

      unchecked {
        ++_i;
      }
    }
  }
```

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L319-L394

```solidity
  function modifyRelayJobs(address _relay, IAutomationVault.JobData[] memory _jobsData) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
    // Create the counters variables
    uint256 _i;
    uint256 _j;

    // Get the list of jobs
    address[] memory _jobs = _approvedJobs[_relay].values();

    // Get the length of the jobs array
    uint256 _jobsLength = _jobs.length;

    // Create the selector length variable
    bytes32[] memory _selectors;

    // Create the length variable
    uint256 _selectorsLength;

    // Remove the jobs
    for (_i; _i < _jobsLength;) {
      _approvedJobs[_relay].remove(_jobs[_i]);

      // Get the length of the selectors array
      _selectorsLength = _approvedJobSelectors[_relay][_jobs[_i]].length();

      // Get the list of selectors
      _selectors = _approvedJobSelectors[_relay][_jobs[_i]].values();

      // Remove the selectors
      for (_j = 0; _j < _selectorsLength;) {
        _approvedJobSelectors[_relay][_jobs[_i]].remove(_selectors[_j]);

        unchecked {
          ++_j;
        }
      }

      unchecked {
        ++_i;
      }
    }

    // Get the length of the jobs data array
    _jobsLength = _jobsData.length;

    // Set the jobs and their selectors for the relay
    for (_i = 0; _i < _jobsLength;) {
      IAutomationVault.JobData memory _jobData = _jobsData[_i];

      // Necessary to avoid an empty job from being assigned to selectors
      if (_jobData.job != address(0)) {
        // Set the job for the relay
        if (_approvedJobs[_relay].add(_jobData.job)) {
          emit ApproveJob(_jobData.job);
        }

        // Get the length of the selectors array
        _selectorsLength = _jobData.functionSelectors.length;

        // Set the selectors for the job
        for (_j = 0; _j < _selectorsLength;) {
          if (_approvedJobSelectors[_relay][_jobData.job].add(_jobData.functionSelectors[_j])) {
            emit ApproveJobSelector(_jobData.job, _jobData.functionSelectors[_j]);
          }

          unchecked {
            ++_j;
          }
        }
      }

      unchecked {
        ++_i;
      }
    }
  }
```

As observed, there is no validation to confirm whether the `_relay` has already been registered. Consequently, the owner of the `AutomationVault` can register `caller`s and `job`s for an unregistered relay, leading to potential issues. For instance, a malicious owner could deceive a victim caller into executing `AutomationVault::exec` using an unregistered relay due to the absence of a check for the relay (in this scenario, `msg.sender`) during `AutomationVault::exec`.

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L397-L452

```solidity
  function exec(address _relayCaller, ExecData[] calldata _execData, FeeData[] calldata _feeData) external {
    // Check that the specific caller is approved to call the relay
    if (!_approvedCallers[msg.sender].contains(_relayCaller) && !_approvedCallers[msg.sender].contains(_ALL)) {
      revert AutomationVault_NotApprovedRelayCaller();
    }

    // Create the exec data needed variables
    ExecData memory _dataToExecute;
    uint256 _dataLength = _execData.length;
    uint256 _i;
    bool _success;

    // Iterate over the exec data to execute the jobs
    for (_i; _i < _dataLength;) {
      _dataToExecute = _execData[_i];

      // Check that the selector is approved to be called
      if (!_approvedJobSelectors[msg.sender][_dataToExecute.job].contains(bytes4(_dataToExecute.jobData))) {
        revert AutomationVault_NotApprovedJobSelector();
      }
      (_success,) = _dataToExecute.job.call(_dataToExecute.jobData);
      if (!_success) revert AutomationVault_ExecFailed();

      // Emit the event
      emit JobExecuted(msg.sender, _relayCaller, _dataToExecute.job, _dataToExecute.jobData);

      unchecked {
        ++_i;
      }
    }

    // Create the fee data needed variables
    FeeData memory _feeInfo;
    _dataLength = _feeData.length;
    _i = 0;

    // Iterate over the fee data to issue the payments
    for (_i; _i < _dataLength;) {
      _feeInfo = _feeData[_i];

      // If the token is the native token, transfer the funds to the receiver, otherwise transfer the tokens
      if (_feeInfo.feeToken == NATIVE_TOKEN) {
        (_success,) = _feeInfo.feeRecipient.call{value: _feeInfo.fee}('');
        if (!_success) revert AutomationVault_NativeTokenTransferFailed();
      } else {
        IERC20(_feeInfo.feeToken).safeTransfer(_feeInfo.feeRecipient, _feeInfo.fee);
      }

      // Emit the event
      emit IssuePayment(msg.sender, _relayCaller, _feeInfo.feeRecipient, _feeInfo.feeToken, _feeInfo.fee);

      unchecked {
        ++_i;
      }
    }
  }
```

## Impact

There may be unregistered relays that have some callers and job data. And in that situation, `AutomationVault::exec` could be implemented through an unregistered relay.

## Code Snippet

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L283-L452

## Tool used

Manual Review

## Recommendation

It is essential to include a check for the relay in `AutomationVault::modifyRelayCallers`, `AutomationVault::modifyRelayJobs` and `AutomationVault::exec`.

```diff
  function modifyRelayCallers(address _relay, address[] memory _callers) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
+   require(_relays.contains(_relay), "Unregistered relay!");
    [...]
  }
```

```diff
  function modifyRelayJobs(address _relay, IAutomationVault.JobData[] memory _jobsData) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
+   require(_relays.contains(_relay), "Unregistered relay!");
    [...]
  }
```

```diff
  function exec(address _relayCaller, ExecData[] calldata _execData, FeeData[] calldata _feeData) external {
+   require(_relays.contains(msg.sender), "Unregistered relay!");
    [...]
  }
```
