Droll Sand Goldfish

medium

# Validation for the _relay parameter is missing in the modifyRelayCallers() and modifyRelayJobs() functions.

## Summary

In `AutomationVault.sol`, the `modifyRelay()` function resets the `_approvedCallers`, `_approvedJobs` and `_approvedJobSelectors`. However, there does not check if the `_relay` exists in `relays` which is the list of approved relays.

## Vulnerability Detail

`modifyRelayCallers()` and `modifyRelayJobs()` functions in [AutomationVault.sol](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol) do not verify whether the `_relay` exists in the relays list, which contains approved relays.

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L283-L316

```solidity
  function modifyRelayCallers(address _relay, address[] memory _callers) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
    // Create the counter variable
    uint256 _i;

    // Get the approved callers
    address[] memory _oldApprovedCallers = _approvedCallers[_relay].values();

    // Get the length of the approved callers array
    uint256 _callersLength = _oldApprovedCallers.length;

    // Remove the callers
    for (_i; _i < _callersLength;) {
      _approvedCallers[_relay].remove(_oldApprovedCallers[_i]);

      unchecked {
        ++_i;
      }
    }

    // Get the length of the callers array
    _callersLength = _callers.length;

    // Set the callers for the relay
    for (_i = 0; _i < _callersLength;) {
      if (_approvedCallers[_relay].add(_callers[_i])) {
        emit ApproveRelayCaller(_relay, _callers[_i]);
      }

      unchecked {
        ++_i;
      }
    }
  }
```

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L319-L394

```solidity
  function modifyRelayJobs(address _relay, IAutomationVault.JobData[] memory _jobsData) public onlyOwner {
    if (_relay == address(0)) revert AutomationVault_RelayZero();
    // Create the counters variables
    uint256 _i;
    uint256 _j;

    // Get the list of jobs
    address[] memory _jobs = _approvedJobs[_relay].values();

    // Get the length of the jobs array
    uint256 _jobsLength = _jobs.length;

    // Create the selector length variable
    bytes32[] memory _selectors;

    // Create the length variable
    uint256 _selectorsLength;

    // Remove the jobs
    for (_i; _i < _jobsLength;) {
      _approvedJobs[_relay].remove(_jobs[_i]);

      // Get the length of the selectors array
      _selectorsLength = _approvedJobSelectors[_relay][_jobs[_i]].length();

      // Get the list of selectors
      _selectors = _approvedJobSelectors[_relay][_jobs[_i]].values();

      // Remove the selectors
      for (_j = 0; _j < _selectorsLength;) {
        _approvedJobSelectors[_relay][_jobs[_i]].remove(_selectors[_j]);

        unchecked {
          ++_j;
        }
      }

      unchecked {
        ++_i;
      }
    }

    // Get the length of the jobs data array
    _jobsLength = _jobsData.length;

    // Set the jobs and their selectors for the relay
    for (_i = 0; _i < _jobsLength;) {
      IAutomationVault.JobData memory _jobData = _jobsData[_i];

      // Necessary to avoid an empty job from being assigned to selectors
      if (_jobData.job != address(0)) {
        // Set the job for the relay
        if (_approvedJobs[_relay].add(_jobData.job)) {
          emit ApproveJob(_jobData.job);
        }

        // Get the length of the selectors array
        _selectorsLength = _jobData.functionSelectors.length;

        // Set the selectors for the job
        for (_j = 0; _j < _selectorsLength;) {
          if (_approvedJobSelectors[_relay][_jobData.job].add(_jobData.functionSelectors[_j])) {
            emit ApproveJobSelector(_jobData.job, _jobData.functionSelectors[_j]);
          }

          unchecked {
            ++_j;
          }
        }
      }

      unchecked {
        ++_i;
      }
    }
  }
```

As a result, if a user uses these functions to modify data for unapproved relays, the `relays` state vaiable would not contain the `_relay` in parameter list and `_approvedJobs`, `_approvedJobSelectors`, _approvedCallers are registered. However, in reality, the callers can still interact with the `AutomationVault` through this `_relay`.

## Impact

It compromises the data integrity of `AutomationVault`, posing potential risks. Specifically, it creates inconsistencies between the frontend and on-chain data. This may lead users to make incorrect decisions.

## Code Snippet

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L283-L316

https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L319-L394

## Tool used

Manual Review

## Recommendation

Validation for `_relay` parameter should be added in `modifyRelayCallers()` and `modifyRelayJobs()`.

```diff
  function modifyRelayCallers(address _relay, address[] memory _callers) public onlyOwner {
-   if (_relay == address(0)) revert AutomationVault_RelayZero();
+   require(_relays.contains(_relay), "NoRelay");
    [...]
  }
```

```diff
  function modifyRelayJobs(address _relay, IAutomationVault.JobData[] memory _jobsData) public onlyOwner {
-   if (_relay == address(0)) revert AutomationVault_RelayZero();
+   require(_relays.contains(_relay), "NoRelay");
    [...]
  }
```
