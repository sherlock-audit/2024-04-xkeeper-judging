Fierce Pear Troll

medium

# A malicious Keeper can temporarily lock automation vault user's bonds

## Summary
A malicious user can register as a valid keeper on https://keep3r.network/ and grief automation jobs created through `AutomationVault.sol` by injecting malicious data in `_execData` in `Keep3rRelay.sol`. 
## Vulnerability Detail

To enable the `Keep3rRelay`, a user has to approve the `worked` and `isKeeper` selectors. Typically, a user would set the approved callers for `Keep3rRelay` as `_ALL`, as they would want any valid Keeper to be able to complete the job ([Refer this](https://docs.xkeeper.network/content/how-to/keep3r_relay.html)). The `isKeeper` function is used to validate the keeper and set the `initialGas` variable, the [`worked`](https://github.com/keep3r-network/keep3r-network-v2/blob/a3b8e0f53e33297a826c98c80538a9351ed29efb/solidity/contracts/peripherals/jobs/Keep3rJobWorkable.sol#L47) function is called after the job is completed and calculates the payment to be made to the Keeper. 
However, a malicious keeper could inject multiple instances of `worked` in the `_execData` argument of the `exec` function within `Keep3rRelay.sol` and extract a payment greater than the deserved amount :
```solidity
function exec(IAutomationVault _automationVault, IAutomationVault.ExecData[] calldata _execData) external {
    // Ensure that calls are being passed
    uint256 _execDataLength = _execData.length;
    if (_execDataLength == 0) revert Keep3rRelay_NoExecData();

    // The first call to `isKeeper` ensures the caller is a valid keeper
    bool _isKeeper = KEEP3R_V2.isKeeper(msg.sender);
    if (!_isKeeper) revert Keep3rRelay_NotKeeper();

    // Create the array of calls which are going to be executed by the automation vault
    IAutomationVault.ExecData[] memory _execDataKeep3r = new IAutomationVault.ExecData[](_execDataLength + 2);

    // The second call sets the initialGas variable inside Keep3r in the same deepness level than the `worked` call
    // If the second call is not done, the initialGas will have a 63/64 more gas than the `worked`, thus overpaying a lot
    _execDataKeep3r[0] = IAutomationVault.ExecData({
      job: address(KEEP3R_V2),
      jobData: abi.encodeWithSelector(IKeep3rV2.isKeeper.selector, msg.sender)
    });

    // Inject to that array of calls the exec data provided in the arguments
    for (uint256 _i; _i < _execDataLength;) {
      if (_execData[_i].job == address(KEEP3R_V2)) revert Keep3rRelay_Keep3rNotAllowed();
      _execDataKeep3r[_i + 1] = _execData[_i];
      unchecked {
        ++_i;
      }
    }

    // Inject the final call which will issue the payment to the keeper
    _execDataKeep3r[_execDataLength + 1] = IAutomationVault.ExecData({
      job: address(KEEP3R_V2),
      jobData: abi.encodeWithSelector(IKeep3rV2.worked.selector, msg.sender)
    });

    // Send the array of calls to the automation vault for it to execute them
    _automationVault.exec(msg.sender, _execDataKeep3r, new IAutomationVault.FeeData[](0));

    // Emit the event
    emit AutomationVaultExecuted(_automationVault, msg.sender, _execDataKeep3r);
  }
```
 This is possible as the `worked` selector is approved in `AutomationVault.sol` and the `worked` function relies on the difference between the `gasLeft()` and `initialGas` (which was set at the start) to calculate the payment. Each time `worked` is called, the payment for the entire transaction up till that point + some extra amount would be payed to the Keeper. It would be possible to move bonds worth multiple times the gas cost of the gas provided this way.
However, this would only lock the funds temporarily, as the `Keeper` has a 14 day window before which they cannot withdraw their accumulated bond. This would be mitigated by _slashing_ the `Keeper`, and reimbursing the vault. The quickness of this process relies on the response time of the `disputer` and the `slasher` roles in the Keep3r network.

Although the loss is temporary, the attacker can make the process of retrieving funds a nightmare by manipulating `_keeper` address in `worked` function. The `_keeper` variable here is the address that receives the bonds. There are a few ways to manipulate this-
1. The attacker could create multiple valid Keeper accounts and use a different `_keeper` address for every `worked` that is injected in `_execData`.
2. The attacker could set `_keeper` to an honest keeper bot on the network and route the funds to them. The mitigation would require the honest keeper to be temporarily disputed and slashed, which would temporarily DOS the keeper.
3. The attacker could set `_keeper` to a Job on the network and route the funds to them. The mitigation would require the Job to be temporarily disputed and slashed, which would temporarily DOS the Job.

A close look at the `worked` function in [Keep3rJobWorkable.sol](https://github.com/keep3r-network/keep3r-network-v2/blob/a3b8e0f53e33297a826c98c80538a9351ed29efb/solidity/contracts/peripherals/jobs/Keep3rJobWorkable.sol) would reveal why this attack is possible. The `_keeper` address can be arbitrarily set to any address without any authorization checks. The success of the function only depends on the amount of bonds held by the job and if it can cover the `_payment` required to be sent to the keeper.

Also, it is is fairly easy to become a valid Keeper(3 day cooldown after registration), so the attacker can easily create multiple valid Keepers.
## Impact
Temporary loss of funds/ DOS
## Code Snippet

## Tool used

Manual Review

## Recommendation
Add a check for `_execData` in `exec` which reverts such malicious calls.