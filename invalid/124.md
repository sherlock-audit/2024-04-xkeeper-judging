Jovial Lime Leopard

medium

# A malicious Vault Job can grief The Keeper, and prevent paying him for executing the Job.

## Summary
Making a call to `KEEP3R_V2::isKeeper` changes `KEEP3R_V2::_initialGas` value, causing less payment to the Keeper firing that Job. 

## Vulnerability Detail
In `Keep3rRelay` and `Keep3rBondedRelay`, the fees are paid using `KEEP3R_V2` contract without using the fees mechanism in the vault itself.

The Relayer makes the first call to [`IKeep3rV2.isKeeper`](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/relays/Keep3rRelay.sol#L37-L40) to initialize the gas (the gas for executing that Job. And in the last it makes a call to [`IKeep3rV2.worked`](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/relays/Keep3rRelay.sol#L52-L55). This is done to calculate the gas used by the keeper to run Jobs functions for a given vault.

`IKeep3rV2.isKeeper` function sets `_initialGas` variable to equal the remaining gas.

[Keep3rJobWorkable::isKeeper](https://etherscan.io/address/0xeb02addCfD8B773A5FFA6B9d1FE99c566f8c44CC#code#F10#L20)
```solidity
  function isKeeper(address _keeper) external override returns (bool _isKeeper) {
    _initialGas = _getGasLeft(); // @audit  set `_initialGas` value to equal the remaining gas before executing 
    if (_keepers.contains(_keeper)) {
      emit KeeperValidation(_initialGas);
      return true;
    }
  }
```

And in the last (after executing the Jobs functions), it makes a call to `IKeep3rV2.worked`. which determines what is the amount of gas consumed to make these transactions (Calling Jobs functions).

[Keep3rJobWorkable::worked](https://etherscan.io/address/0xeb02addCfD8B773A5FFA6B9d1FE99c566f8c44CC#code#F10#L72)
```solidity
  function worked(address _keeper) external override {
    address _job = msg.sender;
    ...

    uint256 _gasLeft = _getGasLeft(); // @audit get the remaining gas
    
    // @audit calculate the payment that the Keeper should take 
    uint256 _payment = _calculatePayment(_gasLeft, _extraGas, _oneEthQuote, _boost);

    ...
  }

```

`_calculatePayment` calculates the payment using the gas used to execute these Jobs functions and it gets the gas by subtracting `_gasLeft` from `_initialGas`.

[Keep3rJobWorkable::_calculatePayment](https://etherscan.io/address/0xeb02addCfD8B773A5FFA6B9d1FE99c566f8c44CC#code#F10#L135)
```solidity
  function _calculatePayment( ... ) internal view returns (uint256 _payment) {
    // @audit We calculate the gas that this keeper consumed as follows `_initialGas - _gasLeft` + extra amount
    uint256 _accountedGas = _initialGas - _gasLeft + _extraGas;
    _payment = (((_accountedGas * _boost) / _BASE) * _oneEthQuote) / 1 ether;
  }
``` 

As we can see, `_initialGas` variable will be modified whenever `isKeeper()` function gets called , and it will not revert if non-keeper calls the function.
```solidity
  function isKeeper(address _keeper) external override returns (bool _isKeeper) {
    _initialGas = _getGasLeft();   // @audit `_initialGas` changed when anyone call the function
    if (_keepers.contains(_keeper)) { ... }
  }
```

So if we said that there are 10 functions to be called from different Jobs by that Keeper, and let's say in the 6th or 7th function, we made a call to `isKeeper()`.  `_initialGas` variable will change, leading to a decrease in the payment as we illustrated. Makes the Keeper Lose money (as he received less than he paid).

The developers prevent the direct execution to `KEEP3R_V2` in the Relayer. But this will not prevent calling `KEEP3R_V2` internally.

[Keep3rRelay::exec](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/relays/Keep3rRelay.sol#L44)
```solidity
  function exec(IAutomationVault _automationVault, IAutomationVault.ExecData[] calldata _execData) external {
    ...

    // Inject to that array of calls the exec data provided in the arguments
    for (uint256 _i; _i < _execDataLength;) {
      // @audit Prevent calling `KEEP3R_V2` functions
      if (_execData[_i].job == address(KEEP3R_V2)) revert Keep3rRelay_Keep3rNotAllowed();
      _execDataKeep3r[_i + 1] = _execData[_i];
      unchecked {
        ++_i;
      }
    }

    ...
  }
```

What can occur is that the Job function itself can make an external call to `KEEP3R_V2::isKeeper`, and it will not revert as we illustrate. And it will set the `_initialGas` at the moment of calling making the attack we illustrated possible.

**An example of how such a Job function can be (simple to illustrate the point)**:
```solidity
    function func() public {
      /* The Logic to do in that Job */
      // And in the last call this function
      keep3rV2.isKeeper(address(1));  
    }
```

**Senarious**:
- Since Keepers will be like Bots, validating the calling path may not be implemented by all of the Keepers, which incentivizes the Job Vault owner to make such a job.
- Vault Owner can only configure one Job that does heavy tasks, and in the last of it make a call to `KEEP3R_V2::isKeeper`, causing consumed gas to decrease significantly, preventing the keeper from taking his deserved payment.
- The Job itself (Contract) can be upgradable, So the Job can be safe, and get called more than one time. And after some time The Job Admin upgraded it to make such a thing. And since bots are already configured to do that Job (as it is safe) they will not notice that.
- And there are more and more scenarios that can make this possible, like changing the next Job logic to deceive the Keeper. But we pointed to the clear ones.


## Impact
- Preventing the keeper from taking the right value of tokens that compensate for the Gas he used
- Vault owner gets his Jobs (different functions) with paying as little as possible. 

## Code Snippet
https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/relays/Keep3rRelay.sol#L44
https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L410-L426

## Tool used
Manual Review

## Recommendation
This issue can be mitigated by reading `KEEP3R_V2::_initialGas` parameter before executing requested Jobs, and after executing. And if it differs take the suitable action (like reverting the function).

> AutomationVault::exec
```diff
  function exec(address _relayCaller, ExecData[] calldata _execData, FeeData[] calldata _feeData) external {
    ...

+   bool isKeeperRelayer = msg.sender == keep3rRelayAddress || msg.sender == keep3rBondedRelayAddress;
+   uint256 xKeeperInitialGas = 0;


    // Iterate over the exec data to execute the jobs
    for (_i; _i < _dataLength;) {
      _dataToExecute = _execData[_i];

+    if (isKeeperRelayer && i = 1) {
+     xKeeperInitialGas = xKeeper._initialGas();
+    }

        ...

+     if (isKeeperRelayer && i == _dataLength - 1) {
+       bool attackOccuared = xKeeperInitialGas != xKeeper._initialGas();
+       if (attackOccuared) {
+         /* Take The suitable Action */
+       }
+     }

      unchecked {
        ++_i;
      }
    }

    ...
  }
```

This mitigation must not be implemented as it is, we make a view on how the mitigation can occur.
1. Some variables are not initialized.
2. `_initialGas` is an internal variable not public (needs to get read from storage).
3. Mitigation is pretty complex and can have further issues.
4. Not a gas efficient.

---

There can be another mitigation but it requires the modification of the `xKeeper` contract itself. where we can only initialize the Gas if the call was a keeper. But since the keeper is already deployer, this can not take place if the contract is not upgradable.



